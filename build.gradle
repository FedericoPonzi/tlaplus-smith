plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
}

group = 'me.fponzi.tlasmith'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

application {
    mainClass = 'me.fponzi.tlasmith.Main'
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
    }
}

dependencies {
    api 'org.lamport:tla2tools:1.8.0-SNAPSHOT'
    implementation 'commons-io:commons-io:2.16.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
            'Main-Class': 'me.fponzi.tlasmith.Main'
        )
    }
}

// Create a fat JAR with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes(
            'Main-Class': 'me.fponzi.tlasmith.Main'
        )
    }
    archiveClassifier = 'all'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}